//    _____  _             _
//   |  __ \| |           (_)
//   | |__) | |_   _  __ _ _ _ __  ___
//   |  ___/| | | | |/ _` | | '_ \/ __|
//   | |    | | |_| | (_| | | | | \__ \
//   |_|    |_|\__,_|\__, |_|_| |_|___/
//                    __/ |
//                   |___/

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.4.2'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'pmd'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.github-pages'

//    _____                            _                 _
//   |  __ \                          | |               (_)
//   | |  | | ___ _ __   ___ _ __   __| | ___ _ __   ___ _  ___  ___
//   | |  | |/ _ \ '_ \ / _ \ '_ \ / _` |/ _ \ '_ \ / __| |/ _ \/ __|
//   | |__| |  __/ |_) |  __/ | | | (_| |  __/ | | | (__| |  __/\__ \
//   |_____/ \___| .__/ \___|_| |_|\__,_|\___|_| |_|\___|_|\___||___/
//               | |
//               |_|

configurations {
    asciidoclet
}

repositories {
    jcenter()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'

    asciidoclet 'org.asciidoctor:asciidoclet:1.+'
    testCompile 'com.nagternal:spock-genesis:0.4.0'
}

//     _____             __ _                       _   _
//    / ____|           / _(_)                     | | (_)
//   | |     ___  _ __ | |_ _  __ _ _   _ _ __ __ _| |_ _  ___  _ __
//   | |    / _ \| '_ \|  _| |/ _` | | | | '__/ _` | __| |/ _ \| '_ \
//   | |___| (_) | | | | | | | (_| | |_| | | | (_| | |_| | (_) | | | |
//    \_____\___/|_| |_|_| |_|\__, |\__,_|_|  \__,_|\__|_|\___/|_| |_|
//                             __/ |
//                            |___/

group = releaseGroup
version = releaseVersion

javadoc {
    options.docletpath = configurations.asciidoclet.files.asType(List)
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.addStringOption "-base-dir", "${projectDir}"
    options.addStringOption "-attribute",
            "name=${project.name}," +
            "version=${releaseVersion}"
}

asciidoctor {
    sourceDir 'src/docs'
    attributes 'source-highlighter': 'prettify',
               toc                 : 'left',
               icons               : 'font',
               toclevels           : 3,
               'pdf-style'         : 'default',
               'releaseVersion'    : releaseVersion
}

//    _______        _
//   |__   __|      | |
//      | | __ _ ___| | _____
//      | |/ _` / __| |/ / __|
//      | | (_| \__ \   <\__ \
//      |_|\__,_|___/_|\_\___/
//
//

task allDocs(type:Copy, dependsOn: [javadoc, asciidoctor]) {
    from "$buildDir/docs/"
    into "$buildDir/asciidoc/html5/"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

//    _____  _     _        _ _           _   _
//   |  __ \(_)   | |      (_) |         | | (_)
//   | |  | |_ ___| |_ _ __ _| |__  _   _| |_ _  ___  _ __
//   | |  | | / __| __| '__| | '_ \| | | | __| |/ _ \| '_ \
//   | |__| | \__ \ |_| |  | | |_) | |_| | |_| | (_) | | | |
//   |_____/|_|___/\__|_|  |_|_.__/ \__,_|\__|_|\___/|_| |_|
//
//

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId releaseGroup
            artifactId 'helios'
            version releaseVersion

            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
 /* Normally taken from ~/.gradle/gradle.properties */
    user         = bintrayUser
    key          = bintrayKey
    publish      = false
    publications = ['mavenCustom']
    pkg {
        repo            = 'maven'
        name            = 'helios'
        desc            =  releaseDescription
        websiteUrl      = 'http://mariogarcia.github.io/helios/'
        issueTrackerUrl = 'https://github.com/mariogarcia/helios/issues'
        licenses        = ['Apache-2.0']
        vcsUrl          = 'https://github.com/mariogarcia/helios.git'
        labels          = ['java', 'validation']
        version {
            name      = releaseVersion
            desc      = 'Helios Release'
            released  = new Date()
            vcsTag    = releaseVersion
        }
    }
}

githubPages {
    repoUri = 'https://github.com/mariogarcia/helios.git'
    deleteExistingFiles = true

    pages {
        from file('build/asciidoc/html5')
    }

    //https://github.com/ajoberstar/gradle-git/wiki/Travis-CI-Authentication
    credentials {
        username = System.getenv('GH_TOKEN')
        password = ''
    }
}
